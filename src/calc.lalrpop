use std::str::FromStr;
use ast::{Statement, Expr, OpCode, IdentReprMode};

grammar;

pub Statement: Statement = {
    <p:Ident> "=" <e: Expr> => Statement::Assign(p, e),
    <Expr> => Statement::Print(<>),
    "?" <Expr> => Statement::Repr(IdentReprMode::Mixed, <>),
    <ReprMode> "?" <Expr> => Statement::Repr(<>),
};

ReprMode: IdentReprMode = {
    "e" => IdentReprMode::Eager,
    "l" => IdentReprMode::Lazy,
    "m" => IdentReprMode::Mixed,
};

Expr = Tier<ExprOp, Factor>;
Factor = Tier<FactorOp, Term>;

Tier<Op, NextTier>: Expr = {
    Tier<Op, NextTier> Op NextTier => Expr::new_op(<>),
    NextTier,
};

ExprOp: OpCode = {
    "+" => OpCode::Add,
    "-" => OpCode::Sub,
};
FactorOp: OpCode = {
    "*" => OpCode::Mul,
    "/" => OpCode::Div,
};

pub Term: Expr = {
    <Num> => Expr::Num(<>),
    <Ident> => Expr::Ident(<>),
    "(" <Expr> ")",
    "resolve" <Term> => Expr::Resolve(Box::new(<>)),
};

Num: i32 = <r"[0-9]+"> => i32::from_str(<>).unwrap();
Ident: String = <r"[[:alpha:]]\w*"> => String::from(<>);
